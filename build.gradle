import org.junit.platform.console.options.Details

buildscript {
    ext {
        commonsLang3Version = '3.7'
        junitJupiterVersion = '5.0.3'
        junitPlatformVersion = '1.0.3'
        springBootVersion = '2.0.0.M7'
        pitestGradlePluginVersion = '1.2.4'
        pitestJunit5PluginVersion = '0.2'
        pitestVersion = '1.2.5'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
    }
    configurations.maybeCreate('pitest')
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}")
        classpath("info.solidsoft.gradle.pitest:gradle-pitest-plugin:${pitestGradlePluginVersion}")
        pitest("org.pitest:pitest-junit5-plugin:${pitestJunit5PluginVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.junit.platform.gradle.plugin'
apply plugin: 'info.solidsoft.pitest'

pitest {
    targetClasses = ['com.example.demo.*']
    outputFormats = ['XML', 'HTML']
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    compile("org.apache.commons:commons-lang3:${commonsLang3Version}")
    compile('org.liquibase:liquibase-core')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')

    runtime('com.h2database:h2')
    runtime('org.postgresql:postgresql')
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        // Exclude JUnit 4
        exclude group: 'junit'
    }
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")
}

junitPlatform {
    details Details.TREE
}

task checkCleanArchitecture(type: Exec) {
    commandLine 'tools/check-clean-architecture.sh'
}
check.dependsOn checkCleanArchitecture

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}