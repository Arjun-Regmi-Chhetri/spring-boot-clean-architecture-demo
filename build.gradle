buildscript {
    ext {
        commonsLang3Version = '3.7'
        junitJupiterVersion = '5.1.0'
        junitPlatformVersion = '1.1.0'
        mockitoVersion = '2.18.0'
        springBootVersion = '2.0.1.RELEASE'
        pitestGradlePluginVersion = '1.3.0'
        pitestJunit5PluginVersion = '0.4'
    }
    repositories {
        mavenCentral()
    }
    configurations.maybeCreate('pitest')
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("info.solidsoft.gradle.pitest:gradle-pitest-plugin:${pitestGradlePluginVersion}")
        pitest("org.pitest:pitest-junit5-plugin:${pitestJunit5PluginVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'info.solidsoft.pitest'

pitest {
    pitestVersion = '1.3.2'
    testPlugin = 'junit5'
    targetClasses = ['com.example.demo.*']
    outputFormats = ['XML', 'HTML']
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.apache.commons:commons-lang3:${commonsLang3Version}")
    compile('org.liquibase:liquibase-core')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')

    runtime('com.h2database:h2')
    runtime('org.postgresql:postgresql')
    runtime('org.springframework.boot:spring-boot-devtools')

    testCompile('org.springframework.boot:spring-boot-starter-test') {
        // Exclude JUnit 4
        exclude group: 'junit'
    }
    testCompile("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testCompile("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    testCompile("org.junit.platform:junit-platform-launcher:${junitPlatformVersion}")

    // These explicit mockito dependencies won't be necessary when Spring Boot will use Mockito >= 2.17 (probably with Spring Boot 2.1)
    testCompile("org.mockito:mockito-core:${mockitoVersion}")
    testCompile("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task checkCleanArchitecture(type: Exec) {
    commandLine 'tools/check-clean-architecture.sh'
}
check.dependsOn checkCleanArchitecture

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
